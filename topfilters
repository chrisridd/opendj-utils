#!/usr/bin/perl -w
# Find the most popular search filters

use strict;
use warnings;

$::VERSION = 0.2;

sub version()
{
	print STDERR "$0 version $::VERSION\n";
	exit 0;
}

sub usage()
{
	print STDERR "Usage: $0\n";
	exit 1;
}

my %opt = (
	'h' => 0,                   # Help
	'v' => 0,                   # Version
);

while (@ARGV) {
	my $arg = shift @ARGV;
	if ($arg =~ m{^-h$|^--help$}) {
		$opt{h} = 1;
	} elsif ($arg =~ m{^-v$|^--version$}) {
		$opt{v} = 1;
	} else {
		push @ARGV, $arg;
		last;
	}
}

version if $opt{v};
usage if $opt{h};

my %filter;

while (<>) {
	chomp;
	if (my ($scope,$filter) = m{SEARCH REQ.* scope=(\S+) filter="([^\"]+)"}) {
		next if $scope =~ m{baseObject};
		$filter = lc($filter);
		$filter =~ s{\(([a-z0-9_-]+)=[^\*\)]+\*?\)}{($1=EQUALITY)}gi;
		$filter =~ s{\(([a-z0-9_-]+)>=[^\*\)]+\)}{($1=ORDERING)}gi;
		$filter =~ s{\(([a-z0-9_-]+)<=[^\*\)]+\)}{($1=ORDERING)}gi;
		$filter =~ s{\(([a-z0-9_-]+)=\*\)}{($1=PRESENCE)}gi;
		$filter =~ s{\(([a-z0-9_-]+)=[^\*]*\*[^\)]*\)}{($1=SUBSTRING)}gi;
		$filter{$filter}++;
	}
}

format STDOUT_TOP =
 frequency filter
.
format =
@>>>>>>>>> ^*
$::freq, $::filt
.

foreach our $filt (sort { $filter{$b} <=> $filter{$a} } keys(%filter)) {
	our $freq = $filter{$filt};
	write;
}

1;

__END__

=head1 NAME

topfilters -  analyze search filters in OpenDJ logs

=head1 SYNOPSIS

topfilters

topfilters -h

topfilters -v

=head1 DESCRIPTION

OpenDJ access log files include the filters used for all search operations.
Filters for baseObject searches are not indexable, but all others are.

This tool outputs a list of filter types, ordered by frequency. Note this does
not mean that the server requires an index for every term, so the output is only
a guide to what might need indexing.

=head1 OPTIONS

=over

=item B<-h> display help text.

=item B<-v> report the tool version.

=back

=head1 EXAMPLE

$ topfilters < access
 frequency filter
      3002 (objectclass=EQUALITY)
       430 (objectclass=PRESENCE)
        25 (ds-task-id=EQUALITY)
         7 (telephonenumber=EQUALITY)
         1 (&(cn=PRESENCE)(telephonenumber=EQUALITY))

=head1 SEE ALSO

L<http://opendj.forgerock.org>

=head1 AUTHOR

Chris Ridd E<lt>chris.ridd@forgerock.comE<gt>

=head1 COPYRIGHT

Copyright (c) 2013-2022 Chris Ridd. All rights reserved. This tool is free software;
you can redistribute it and/or modify it under the same terms as Perl itself.
